import "../utils.just"
wrangler := "npx wrangler"
default:
    just --list

login:
    {{wrangler}} login

init: 
    @just _utils-check-program "volta" 0
    cd ../global-frontend-dependencies/ && pnpm install
    cd html && pnpm install

dev:
    cd html && pnpm serve

bundle-html:
    {{if env("INIT_PAGES","0") == "0" {"cd ../pages && just frontend-build"} else { "" } }}
    cd html && pnpm build

build-html: bundle-html
    @rm -rf assets/dist
    @mkdir -p assets/dist
    cp html/dist/* assets/dist/
    @rm -f assets/dist/menu.html
    @rm -f assets/dist/base-styles.bundle.js
    @shopt -s globstar && rm -f assets/dist/**/*.br assets/dist/**/*.gz
    {{ if env("INIT_PAGES","0") == "1" {"exit 1"} else {""} }}
    @mkdir -p assets/dist/public-pages/home

    cp ../pages/dist/public-pages/home/main.* assets/dist/public-pages/home/
    cp ../pages/dist/public-pages/home/obtaining-email.* assets/dist/public-pages/home/
    cp ../pages/dist/public-pages/home/pulls.* assets/dist/public-pages/home/
    cp ../pages/dist/public-pages/home/eponge.* assets/dist/public-pages/home/
    
    @if [ -d "../pages/dist/hxi2-private-pages" ]; then \
        mkdir -p assets/dist/hxi2-private-pages; \
        cp -r ../pages/dist/hxi2-private-pages/* assets/dist/hxi2-private-pages/; \
    fi
    @shopt -s globstar && rm -f assets/dist/**/*.br assets/dist/**/*.gz

    @mv assets/dist/public-pages/home/main.html assets/index.html
    @mv assets/dist/public-pages/home/pulls.html assets/pulls.html
    @mv assets/dist/public-pages/home/eponge.html assets/eponge.html
    @mv assets/dist/public-pages/home/obtaining-email.html assets/obtaining-email.html

    @if [ -d "../pages/dist/hxi2-private-pages" ]; then \
        mv assets/dist/hxi2-private-pages/accepte.html assets/accepte.html; \
    fi

push: compile-thumbnails build-html
    {{wrangler}} deploy

compile-thumbnails:
    @just _utils-check-program "cwebp" 2
    @just _utils-check-program "fd" 2
    fd --type=file "\.(jpg|png)$" assets/pulls/ -x bash -c '[ -f "{}.webp" ] || cwebp -q 80 {} -o "{}.webp"'